# 程序设计基础

例1：

> 1.把**程序存储器**中TAB开始的若干个字节数据,转储到内部数据存储器50H开始的存储区域。

```asm
SJMP begin

ORG 0030H
	
begin:
	MOV dptr, #tab   ; 先把TAB开始的地址送到dptr
	MOV R7, #10      ; 控制循环，循环10次（移动10个数据）
	MOV R0, #50H     ; 把目标地址送到R0
L0:
	CLR A            ; 先清空A
	MOVC A, @A+dptr  ; 查表，把DPTR指向的内容给A
	MOV @R0, A       ; 把A的内容给R0指向的单元
	INC R0            
	INC dptr         ; 目标的地址和 dptr 的地址加一，移动下一个元素
	DJNZ R7, L0      ; R7减1后判断是否为0，不为0返回到L0循环
	SJMP $
TAB:
	DB 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15
	
```

例2：

> 2.将单片机内RAM中若干单字节无符号的整数,按从小到大的次序重新排序。设排序50~5AH单元数据(冒泡法)

```asm
FLAG EQU PSW.5     ; 标志一遍循环有没有交换过
ary DATA 50H
num DATA 15

SORT:
    PUSH PSW       ; 保护现场
    PUSH ACC
    PUSH B
    SETB RS0
    CLR RS1
    MOV R7, #num   ; 要排序的数的个数
    DEC R7
    MOV A, R7
    MOV R6, A
PX: MOV R0, #ary   ; 要排序的数组
    MOV A, R6
    MOV R7, A
    CLR FLAG
pp:
    MOV A, @R0     ; 取第一个数放在A
    INC R0          
    MOV B, @R0     ; 取第二个数放在寄存器B
    CJNE A, B, jk  ; 比较A和B，若AB不相等，跳转至jk，若A < B, 进位标志 CY = 1
    SJMP next      ; 说明AB相等，执行 next
jk:
    JC next        ; 如果 CY = 1(前一个数小于后一个数)不需要交换，跳转至next,
    XCH A, @R0     ; 交换A和R0(第二个数)的值
    DEC R0         ; R0 回到第一个数
    MOV @R0, A     ; (R0) <-- A
    INC R0         ; 恢复 R0
    SETB FLAG      ; 只要发生交换，FLAG置为1，表示没有排序完
next:
    DJNZ R7, pp    ; 控制第二层循环结束
    DEC R6
    MOV A, R6
    JZ exit
    JB FLAG, px    ; 控制第一层循环，如果一遍循环没有进行以此交换，说明已经排好序了
exit:
    POP B          ; 恢复现场
    POP ACC
    POP PSW
    RET
```

例3:

> 3.编制子程序,实现双字节无符号数相乘R2R3*R6R7=>R4R5R6R7,要求:除了传出参数外,其他资源不能修改;
>
> 程序入口:R2、R3为被乘数,R6、R7为乘数
> 程序出口:R4、R5、R6、R7为乘积

![UTOOLS1589620991988.png](http://yanxuan.nosdn.127.net/2e632fcb4cce7cc84ca4c8cee5d21165.png)

